\newpage
\section{控制系統設計與結果}
	最終的實體作品，我們選用Arduino UNO 作為開發板，VL53L0作為傳感器輸入，MG 996R伺服馬達進行輸出。

\lstset{
  language=C++,                % 設定語言
  basicstyle=\ttfamily\small,  % 設定字型和字體大小
  keywordstyle=\color{blue},   % 關鍵字顏色
  commentstyle=\color{darkgray},   % 註釋顏色
  stringstyle=\color{red},     % 字串顏色
  numbers=left,                % 行號顯示位置
  numberstyle=\tiny\color{gray}, % 行號字型和顏色
  stepnumber=1,                % 行號的間隔
  numbersep=5pt,               % 行號與程式碼的距離
  showspaces=false,            % 是否顯示空格
  showstringspaces=false,      % 是否顯示字串中的空格
  tabsize=2,                   % Tab 的大小
  breaklines=true,             % 自動換行
  breakatwhitespace=false,     % 只在空白處換行
  frame=single,                % 設定邊框樣式
  captionpos=b,                % 標題位置
  escapeinside={\%*}{*)},      % 可用於在程式碼中插入 LaTeX 命令
  morekeywords={*,...}         % 如果有額外的關鍵字，可以在這裡加入
}


\subsection{控制系統}
Arduino板的控制程式如下:





\subsubsection{導入函式庫}
\begin{lstlisting}[language=C]
#include <Wire.h>       // 包含 I2C 通信的函式庫
#include <VL53L0X.h>    // 包含 VL53L0X 距離感測器的函式庫
#include <Servo.h>      // 包含控制伺服馬達的函式庫
\end{lstlisting}

\subsubsection{宣告物件和常數}
\begin{lstlisting}[language=C]
// 宣告物件
VL53L0X sensor; // 宣告一個 VL53L0X 類別的物件，稱為 sensor
Servo motor;    // 宣告一個 Servo 類別的物件，稱為 motor

// PID 控制常數
const float kp = 4.0;  // 比例增益
const float ki = 2;    // 積分增益
const float kd = 3.0;  // 微分增益
const float tt = 1000; // 時間延遲常數，單位為毫秒

// 初始化誤差和積分項
float error_sum = 0.0;         // 誤差積分項的初始值
float last_error = 0.0;        // 上一次的誤差
unsigned long last_time = 0;   // 上一次的時間
\end{lstlisting}

\subsubsection{初始化設定}
\begin{lstlisting}[language=C]
void setup() {
  // 設置串口通信速率
  Serial.begin(115200);  // 設置串口通信速率為115200
  Wire.begin();          // 初始化 I2C 通信
  
  // 初始化感測器
  sensor.setTimeout(500);  // 設置感測器超時時間為500毫秒
  if (!sensor.init()) {    // 初始化感測器，如果失敗則進入循環
    Serial.println("Failed to detect and initialize sensor!");
    while (1);  // 如果感測器初始化失敗，停止程式
  }
  sensor.startContinuous();  // 開始連續測量
  
  // 初始化伺服馬達
  motor.attach(9);  // 連接馬達到9號引腳
  motor.write(90);  // 將馬達設置到90度，即中間位置
  delay(tt);        // 延遲一段時間以確保馬達回到中間位置
  
  last_time = micros();  // 初始化時間
}
\end{lstlisting}

\subsubsection{獲取距離值}
\begin{lstlisting}[language=C]
float getDistance() {
  float distance = sensor.readRangeContinuousMillimeters(); // 讀取距離，單位毫米
  if (sensor.timeoutOccurred()) {
    Serial.print("Timeout");  // 如果超時，輸出提示
    return -1;  // 返回 -1 表示發生超時
  } else {
    // 限制距離範圍在35到300毫米之間
    if (distance < 35) {
      distance = 35;
    } else if (distance > 300) {
      distance = 300;
    }
    return distance;  // 返回有效的距離值
  }
}
\end{lstlisting}

\subsubsection{控制馬達}
\begin{lstlisting}[language=C]
void controlMotor(float target_distance, float dt) {
  float distance = getDistance();  // 獲取距離
  if (distance > 0) {  // 如果距離值有效
    // 輸出目標距離和實際距離
    Serial.print("Target: ");
    Serial.print(target_distance);
    Serial.print(" mm Distance: ");
    Serial.print(distance);

    // 計算誤差
    float error = target_distance - distance;
    Serial.print(" Error: ");  // 輸出誤差
    Serial.print(error);

    // 更新誤差總和
    error_sum += error * dt;

    // 防止積分項積累過多
    error_sum = constrain(error_sum, -100, 100);

    // 計算微分項
    float derivative = (error - last_error) / dt;

    // 計算控制信號
    float control_signal = kp * error + ki * error_sum + kd * derivative;
    if (control_signal < 30) {  // 如果控制信號小於30，不使用積分項
      control_signal = kp * error + kd * derivative;
      Serial.print(" NO KI");
    }
    // 將控制信號映射到有效的角度範圍內
    int angle = constrain(map(control_signal, -1000, 1000, 180, 0), 0 , 180);
    Serial.print(" Control_signal: ");  // 輸出控制信號
    Serial.print(control_signal);
    Serial.print(" angle: ");  // 輸出馬達角度
    Serial.println(angle);
    motor.write(angle);  // 設置馬達角度

    // 更新上一次的誤差
    last_error = error;
  }
}
\end{lstlisting}

\subsubsection{主迴圈}
\begin{lstlisting}[language=C]
void loop() {
  // 檢查是否有串口輸入
  if (Serial.available() > 0) {
    char key = Serial.read();  // 讀取串口輸入
    if (key == 'q') {  // 如果收到 'q' 指令
      motor.detach();  // 停止馬達
      while (1);  // 停止程式
    }
  }

  // 計算時間差
  unsigned long current_time = micros();  // 獲取當前時間（微秒）
  float dt = (current_time - last_time) / 1000000.0; // 計算時間差，並將微秒轉換為秒
  last_time = current_time;  // 更新上一次的時間

  controlMotor(90, dt);  // 設定目標距離為 90 mm

  delay(100);  // 延遲以匹配時間步長 (dt)
}
\end{lstlisting}

\subsection{設計結果}
在最終的版本由於紅外線時常檢測不到體積較小的鋼球，所以我們採用體積更大的乒乓球來代替鋼球使整體系統更加完善，而系統控制的部分我們藉由調整PID參數已盡可能讓系統趨近穩定，但穩定過後還是會出現些微的震盪，我們猜測這可能是因為3D列印品質不佳的公差所導致。



